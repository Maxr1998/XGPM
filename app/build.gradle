apply plugin: 'com.android.application'

android {
    signingConfigs {
        main
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    defaultConfig {
        applicationId "de.Maxr1998.xposed.gpm"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 25
        versionName "1.9.2"
        resConfigs "en", "it"
        vectorDrawables {
            generatedDensities = ["hdpi", "xhdpi", "xxhdpi", "xxxhdpi"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.main
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-conf.txt'
            minifyEnabled true
            shrinkResources true
            debuggable false
        }
        debug {
            signingConfig signingConfigs.main
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

def propFile = project.file("keyStore.properties")
if (propFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propFile))
    android.signingConfigs.main.storeFile project.file(props.keyStore)
    android.signingConfigs.main.storePassword props.keyStorePassword
    android.signingConfigs.main.keyAlias props.keyAlias
    android.signingConfigs.main.keyPassword props.keyPassword
}

repositories {
    jcenter()
}
dependencies {
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:palette-v7:24.2.0'
    compile 'de.Maxr1998:track-selector-lib:1.1'
    provided 'de.robv.android.xposed:api:81'
    provided 'de.robv.android.xposed:api:81:sources'
}